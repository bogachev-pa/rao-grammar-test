grammar org.xtext.example.rao.RAO with org.eclipse.xtext.xbase.Xbase

generate rAO "http://www.xtext.org/example/rao/RAO"

import "http://www.eclipse.org/xtext/xbase/Xbase"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

Model:
	{Model} objects += Entity*
;

Entity
	: ResourceType
	| ResourceDeclaration
	| ConstantDeclaration
	| FunctionDeclaration
;

ResourceType:
	'type' name = QualifiedName '{'
		(params += VariableDeclaration ';')+
	'}'
;

VariableDeclaration:
	variable = FullJvmFormalParameter ('=' right = XExpression)?
;

ResourceDeclaration:
	'resource' name = QualifiedName '=' constructor = ResourceConstructorCall ';'
;

ResourceConstructorCall returns XExpression:
	{XConstructorCall}
	constructor = [types::JvmConstructor | QualifiedName]
	explicitConstructorCall ?= '('
		(arguments += XExpression (',' arguments += XExpression)* )?
	')'
;

ConstantDeclaration:
	'const' constant = VariableDeclaration ';'
;

FunctionDeclaration:
	type = JvmTypeReference name = QualifiedName '('
		(params += FullJvmFormalParameter (
			',' params += FullJvmFormalParameter)*)?
	')' body = XBlockExpression
;
